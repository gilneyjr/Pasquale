//Problema 6

ESTRUTURA NODE
    INTEIRO valor, nivel;
    PONTEIRO NODE esq, dir;
FIMESTRUTURA

PROCEDIMENTO insere RECEBE (PONTEIRO NODE ptr, INTEIRO val, INTEIRO nivel)
    nivel++;
    NODE node := VALOR(ptr);
    SE val <= node.valor ENTAO
        SE node.esq /= NULO ENTAO
            insere(node.esq, val, nivel);
        SENAO
            NODE temp;
            temp.valor := val;
            temp.nivel := nivel;
            node.esq := NOVO NODE;
            //ESCREVA("Filho esquerdo do: " + (TEXTO)node.valor + "\n");
            VALOR(node.esq) := temp;
        FIMSE
    SENAO
        SE node.dir /= NULO ENTAO
            insere(node.dir, val, nivel);
        SENAO
            NODE temp;
            temp.valor := val;
            temp.nivel := nivel;
            node.dir := NOVO NODE;
            //ESCREVA("Filho direito do: " + (TEXTO)node.valor + "\n");
            VALOR(node.dir) := temp;
        FIMSE
    FIMSE
    VALOR(ptr) := node;
FIMPROCEDIMENTO

PROCEDIMENTO get_min RECEBE (PONTEIRO NODE ptr, INTEIRO nivel)
    SE ptr = NULO ENTAO
        ESCREVA("Árvore vazia!\n");
        RETORNE;
    FIMSE
   
    NODE cur := VALOR(ptr);
    SE cur.esq /= NULO ENTAO
        get_min(cur.esq, nivel + 1);
    SENAO
        ESCREVA((TEXTO)cur.valor + " (nivel: " + (TEXTO)cur.nivel + ")\n");
    FIMSE
FIMPROCEDIMENTO

PROCEDIMENTO get_max RECEBE (PONTEIRO NODE ptr, INTEIRO nivel)
    SE ptr = NULO ENTAO
        ESCREVA("Árvore vazia!\n");
        RETORNE;
    FIMSE
    
    NODE cur := VALOR(ptr);
    SE cur.dir /= NULO ENTAO
        get_max(cur.dir, nivel + 1);
    SENAO
        ESCREVA((TEXTO)cur.valor + " (nivel: " + (TEXTO)cur.nivel + ")\n");
    FIMSE
FIMPROCEDIMENTO

PROCEDIMENTO apagaFilhos RECEBE (NODE node)
    SE node.esq /= NULO ENTAO
        apagaFilhos(VALOR(node.esq));
        DELETE node.esq;
    FIMSE
    
    SE node.dir /= NULO ENTAO
        apagaFilhos(VALOR(node.dir));
        DELETE node.dir;
    FIMSE
FIMPROCEDIMENTO

PRINCIPAL
    PONTEIRO NODE raiz;
    ENQUANTO VERDADEIRO EXECUTE
        
        ESCREVA("Digite a quantidade de elementos (negativo para sair):\n");
        
        INTEIRO n;
        LEIA(n);
        SE n < 0 ENTAO
            SAIA;
        FIMSE
        
        INTEIRO i := 1;
        
        ENQUANTO i <= n EXECUTE
            INTEIRO v;
            ESCREVA("Digite o elemento " + (TEXTO)i + ": \n");
            LEIA(v);
            SE raiz = NULO ENTAO
                NODE temp;
                temp.valor := v;
                temp.nivel := 1;
                raiz := NOVO NODE;
                VALOR(raiz) := temp;
            SENAO
                insere(raiz, v, 1);
            FIMSE
            i++;
        FIMENQUANTO
        
        ESCREVA("Mínimo: ");
        get_min(raiz, 1);
        
        ESCREVA("Máximo: ");
        get_max(raiz, 1);
        
        NODE fila[n];
        INTEIRO j := 1, ult_nvl := 0;
        
        SE raiz /= NULO ENTAO
            fila[j] := VALOR(raiz);
            j++;
        FIMSE
        
        i := 1;
        ENQUANTO i <= n EXECUTE
            NODE cur := fila[i];
            SE cur.nivel > ult_nvl ENTAO
                ult_nvl := cur.nivel;
                SE ult_nvl > 1 ENTAO
                    ESCREVA("\n");
                FIMSE
                ESCREVA("Nivel " + (TEXTO)ult_nvl + ":");
            FIMSE
            ESCREVA(" " + (TEXTO)cur.valor);
            SE cur.esq /= NULO ENTAO
                fila[j] := VALOR(cur.esq);
                j++;
            FIMSE
            
            SE cur.dir /= NULO ENTAO
                fila[j] := VALOR(cur.dir);
                j++;
            FIMSE
            i++;
        FIMENQUANTO
        ESCREVA("\n");
        
        SE raiz /= NULO ENTAO
            apagaFilhos(VALOR(raiz));
            DELETE raiz;
            raiz := NULO;
        FIMSE
        
    FIMENQUANTO
    ESCREVA("Fim\n");
FIMPRINCIPAL

