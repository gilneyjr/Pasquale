//Problema 6

ESTRUTURA NODE
    INTEIRO valor, nivel;
    PONTEIRO NODE esq, dir;
FIMESTRUTURA

PROCEDIMENTO insere RECEBE (PONTEIRO NODE ptr, INTEIRO val, INTEIRO nivel)
    nivel++;
    SE val <= VALOR(ptr).valor ENTAO
        SE VALOR(ptr).esq /= NULO ENTAO
            insere(VALOR(ptr).esq, val, nivel);
        SENAO
            VALOR(ptr).esq := NOVO NODE;
            VALOR(VALOR(ptr).esq).valor := val;
            VALOR(VALOR(ptr).esq).nivel := nivel;
        FIMSE
    SENAO
        SE VALOR(ptr).dir /= NULO ENTAO
            insere(VALOR(ptr).dir, val, nivel);
        SENAO
            VALOR(ptr).dir := NOVO NODE;
            VALOR(VALOR(ptr).dir).valor := val;
            VALOR(VALOR(ptr).dir).nivel := nivel;
        FIMSE
    FIMSE
FIMPROCEDIMENTO

PROCEDIMENTO get_min RECEBE (PONTEIRO NODE ptr, INTEIRO nivel)
    SE VALOR(ptr).esq /= NULO ENTAO
        get_min(VALOR(ptr).esq, nivel + 1);
    SENAO
        ESCREVA((TEXTO)VALOR(ptr).valor + " (nivel: " + (TEXTO)VALOR(ptr).nivel + ")\n");
    FIMSE
FIMPROCEDIMENTO

PROCEDIMENTO get_max RECEBE (PONTEIRO NODE ptr, INTEIRO nivel)
    SE VALOR(ptr).dir /= NULO ENTAO
        get_max(VALOR(ptr).dir, nivel + 1);
    SENAO
        ESCREVA((TEXTO)VALOR(ptr).valor + " (nivel: " + (TEXTO)VALOR(ptr).nivel + ")\n");
    FIMSE
FIMPROCEDIMENTO

PRINCIPAL
    PONTEIRO NODE raiz;
    ESCREVA("Digite a quantidade de elementos: ");
    INTEIRO n;
    LEIA(n);
    SE n <= 0 ENTAO
        ESCREVA("O número deve ser positivo.\n");
        RETORNE;
    FIMSE
    INTEIRO i := 1;
    
    ENQUANTO i <= n EXECUTE
        INTEIRO v;
        ESCREVA("Digite o elemento " + (TEXTO)i + ": \n");
        LEIA(v);
        SE raiz = NULO ENTAO
            raiz := NOVO NODE;
            VALOR(raiz).valor := v;
            VALOR(raiz).nivel := 1;
        SENAO
            insere(raiz, v, 1);
        FIMSE
        i++;
    FIMENQUANTO
    
    ESCREVA("Mínimo: ");
    get_min(raiz, 1);
    
    ESCREVA("Máximo: ");
    get_max(raiz, 1);
    
    NODE fila[n];
    INTEIRO j := 1, ult_nvl := 0;
    
    fila[j] := VALOR(raiz);
    j++;
    
    i := 1;
    ENQUANTO i <= n EXECUTE
        SE fila[i].nivel > ult_nvl ENTAO
            ult_nvl := fila[i].nivel;
            SE ult_nvl > 1 ENTAO
                ESCREVA("\n");
            FIMSE
            ESCREVA("Nivel " + (TEXTO)ult_nvl + ":");
        FIMSE
        ESCREVA(" " + (TEXTO)fila[i].valor);
        SE fila[i].esq /= NULO ENTAO
            fila[j] := VALOR(fila[i].esq);
            j++;
        FIMSE
        
        SE fila[i].dir /= NULO ENTAO
            fila[j] := VALOR(fila[i].dir);
            j++;
        FIMSE
        i++;
    FIMENQUANTO
    ESCREVA("\n");
    
    ESCREVA("Fim\n");
FIMPRINCIPAL

