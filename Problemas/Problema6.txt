//Problema 6

ESTRUTURA NODE
    INTEIRO valor, nivel;
    PONTEIRO NODE esq, dir;
FIMESTRUTURA

FUNCAO insere RECEBE (PONTEIRO NODE ptr, INTEIRO val, INTEIRO nivel) RETORNA PONTEIRO NODE
    SE ptr = NULO ENTAO
        ptr := NOVO NODE;
        VALOR(ptr).valor := val;
        VALOR(ptr).nivel := nivel;
        RETORNE ptr;
    FIMSE
    
    SE val <= VALOR(ptr).valor ENTAO
        VALOR(ptr).esq := insere(VALOR(ptr).esq, val, nivel+1);
        RETORNE ptr;
    SENAO
        VALOR(ptr).dir := insere(VALOR(ptr).dir, val, nivel+1);
        RETORNE ptr;
    FIMSE
    
    RETORNE ptr;
FIMFUNCAO

PROCEDIMENTO get_min RECEBE (PONTEIRO NODE ptr, INTEIRO nivel)
    SE ptr = NULO ENTAO
        ESCREVA("Árvore vazia!\n");
        RETORNE;
    FIMSE
   
    SE VALOR(ptr).esq /= NULO ENTAO
        get_min(VALOR(ptr).esq, nivel + 1);
    SENAO
        ESCREVA((TEXTO)VALOR(ptr).valor + " (nivel: " + (TEXTO)VALOR(ptr).nivel + ")\n");
    FIMSE
FIMPROCEDIMENTO

PROCEDIMENTO get_max RECEBE (PONTEIRO NODE ptr, INTEIRO nivel)
    SE ptr = NULO ENTAO
        ESCREVA("Árvore vazia!\n");
        RETORNE;
    FIMSE
    
    SE VALOR(ptr).dir /= NULO ENTAO
        get_max(VALOR(ptr).dir, nivel + 1);
    SENAO
        ESCREVA((TEXTO)VALOR(ptr).valor + " (nivel: " + (TEXTO)VALOR(ptr).nivel + ")\n");
    FIMSE
FIMPROCEDIMENTO

PROCEDIMENTO apaga RECEBE (PONTEIRO NODE ptr)
    SE ptr = NULO ENTAO
        RETORNE;
    FIMSE

    apaga(VALOR(ptr).esq);
    apaga(VALOR(ptr).dir);
    
    DELETE ptr;
FIMPROCEDIMENTO

PRINCIPAL
    PONTEIRO NODE raiz;
    
    ENQUANTO VERDADEIRO EXECUTE
        
        ESCREVA("Digite a quantidade de elementos (negativo para sair):\n");
        
        INTEIRO n;
        LEIA(n);
        SE n < 0 ENTAO
            SAIA;
        FIMSE
        
        INTEIRO i := 1;
        
        ENQUANTO i <= n EXECUTE
            INTEIRO v;
            ESCREVA("Digite o elemento " + (TEXTO)i + ": \n");
            LEIA(v);
            raiz := insere(raiz, v, 1);
            i++;
        FIMENQUANTO
        
        ESCREVA("Mínimo: ");
        get_min(raiz, 1);
        
        ESCREVA("Máximo: ");
        get_max(raiz, 1);
        
        SE n = 0 ENTAO
            CONTINUE;
        FIMSE
        
        NODE fila[n];
        INTEIRO j := 1, ult_nvl := 0;
        
        fila[j] := VALOR(raiz);
        j++;
        
        i := 1;
        ENQUANTO i <= n EXECUTE
            NODE cur := fila[i];
            SE cur.nivel > ult_nvl ENTAO
                ult_nvl := cur.nivel;
                SE ult_nvl > 1 ENTAO
                    ESCREVA("\n");
                FIMSE
                ESCREVA("Nivel " + (TEXTO)ult_nvl + ":");
            FIMSE
            ESCREVA(" " + (TEXTO)cur.valor);
            SE cur.esq /= NULO ENTAO
                fila[j] := VALOR(cur.esq);
                j++;
            FIMSE
            
            SE cur.dir /= NULO ENTAO
                fila[j] := VALOR(cur.dir);
                j++;
            FIMSE
            i++;
        FIMENQUANTO
        ESCREVA("\n");
        
        apaga(raiz);
        raiz := NULO;
        
    FIMENQUANTO
    ESCREVA("Fim\n");
FIMPRINCIPAL

