//Problema 3

PRINCIPAL
    INTEIRO lA, cA, lB,cB;

    ESCREVA("Digite a quantidade de linhas da primeira matriz: \n");
    LEIA(lA);
    ESCREVA("Digite a quantidade de colunas da primeira matriz: \n");
    LEIA(cA);
    ESCREVA("Digite a quantidade de linhas da segunda matriz: \n");
    LEIA(lB);
    ESCREVA("Digite a quantidade de colunas da segunda matriz: \n");
    LEIA(cB);

    INTEIRO a[lA,cA];
    INTEIRO b[lB,cB];

    //leitura da primeira matriz
    INTEIRO i := 1, j;
    ESCREVA("Digite os elementos da primeira matriz:\n");
    ENQUANTO  i <= lA EXECUTE
        j := 1;
        ENQUANTO j <= cA EXECUTE
            LEIA(a[i,j]);
            j++;
        FIMENQUANTO
        i++;
    FIMENQUANTO
    
    //leitura da segunda matriz
    i := 1;
    ESCREVA("Digite os elementos da segunda matriz:\n");
    ENQUANTO i <= lB EXECUTE
        j := 1;
        ENQUANTO j <= cB EXECUTE
            LEIA(b[i,j]);
            j++;
        FIMENQUANTO
        i++;
    FIMENQUANTO

    //Soma das matrizes
    SE lA = lB E cA = cB ENTAO
        i:= 1;
        ESCREVA("Resultado da soma das matrizes: \n");
        ENQUANTO i <= lA EXECUTE
            j := 1;
            ENQUANTO j <= cA EXECUTE
                ESCREVA((TEXTO) (a[i,j] + b[i,j])  + " ");
                j++;
            FIMENQUANTO
            ESCREVA("\n");
            i++;
        FIMENQUANTO
    SENAO
        ESCREVA("Não é possível somar essas matrizes.\n");
    FIMSE

    //Multiplicação das matrizes
    SE cA = lB ENTAO
        i:= 1;
        ESCREVA("Resultado da multiplicação das matrizes: \n");
        ENQUANTO i <= lA EXECUTE
            j := 1;
            ENQUANTO j <= cB EXECUTE
                INTEIRO k := 1, soma;
                ENQUANTO k <= cA EXECUTE
                    soma := soma + a[i,k] * b[k,j];
                    k++;
                FIMENQUANTO
                ESCREVA((TEXTO) soma  + " ");
                j++;
            FIMENQUANTO
            ESCREVA("\n");
            i++;
        FIMENQUANTO
    SENAO
        ESCREVA("Não é possível multiplicar essas matrizes.\n");
    FIMSE
FIMPRINCIPAL

